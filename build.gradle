buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.2.RELEASE")
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

subprojects {
    apply plugin: "java"

    repositories {
        mavenCentral()
    }

    version = "0.1"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    if (["front", "accounts", "transactions"].contains(project.name)) {
        apply plugin: "spring-boot"
        apply plugin: "docker"

        dependencies {
            compile("org.springframework.boot:spring-boot-starter-web") {
                exclude module: "spring-boot-starter-tomcat"
            }
            compile "org.springframework.boot:spring-boot-starter-jetty"
            // The following dependency is included if monitoring is required
            // compile "org.springframework.boot:spring-boot-starter-actuator"

            compile("com.fasterxml.jackson.core:jackson-databind")

            testCompile "junit:junit"
        }

        docker {
            baseImage "java:8"
            maintainer "Dmitry Dobrynin <dmitry.v.dobrynin@gmail.com>"
        }

        task buildDockerImage(type: Docker, dependsOn: build) {
            addFile(tasks.jar.archivePath, "/")
            defaultCommand(["java", "-jar", "/${tasks.jar.archiveName}"])
        }
    }
}

